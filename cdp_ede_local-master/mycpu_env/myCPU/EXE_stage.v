`include "mycpu_head.v"

module EXE_stage(
    input  wire                          clk,
    input  wire                          reset,
    // allowin
    input  wire                          ms_allowin,
    output wire                          es_allowin,
    // from id stage
    input  wire                          ds_to_es_valid,
    input  wire [`DS_TO_ES_BUS_WD -1:0]  ds_to_es_bus,
    // to mem stage
    output wire                          es_to_ms_valid,
    output wire [`ES_TO_MS_BUS_WD -1:0]  es_to_ms_bus,
    // data sram interface
    output wire                          data_sram_en,
    output wire [ 3:0]                   data_sram_we,
    output wire [31:0]                   data_sram_addr,
    output wire [31:0]                   data_sram_wdata
);

reg         es_valid;
wire        es_ready_go;

reg  [`DS_TO_ES_BUS_WD-1:0] ds_to_es_bus_r;

wire [11:0] alu_op;
wire        load_op;
wire        src1_is_pc;
wire        src2_is_imm;
wire        src2_is_4;
wire        gr_we;
wire        mem_we;
wire [ 4:0] dest;
wire [31:0] imm;
wire [31:0] rj_value;
wire [31:0] rkd_value;
wire [31:0] es_pc;
wire        res_from_mem;

wire [31:0] alu_src1;
wire [31:0] alu_src2;
wire [31:0] alu_result;

assign {alu_op      ,
        load_op     ,
        src1_is_pc  ,
        src2_is_imm ,
        src2_is_4   ,
        gr_we       ,
        mem_we      ,
        dest        ,
        imm         ,
        rj_value    ,
        rkd_value   ,
        es_pc       ,
        res_from_mem
       } = ds_to_es_bus_r;

assign alu_src1 = src1_is_pc  ? es_pc[31:0] : rj_value;
assign alu_src2 = src2_is_imm ? imm : rkd_value;

alu u_alu(
    .alu_op     (alu_op    ),
    .alu_src1   (alu_src1  ),
    .alu_src2   (alu_src2  ),
    .alu_result (alu_result)
);

assign es_ready_go    = 1'b1;
assign es_allowin     = !es_valid || es_ready_go && ms_allowin;
assign es_to_ms_valid = es_valid && es_ready_go;

assign es_to_ms_bus = {res_from_mem, // 70
                       gr_we       , // 69
                       dest        , // 68:64
                       alu_result  , // 63:32
                       es_pc         // 31:0
                      };
    
assign data_sram_en    = es_valid && (load_op || mem_we);
assign data_sram_we    = (es_valid && mem_we) ? 4'hf : 4'h0;
assign data_sram_addr  = alu_result;
assign data_sram_wdata = rkd_value;

always @(posedge clk) begin
    if (reset) begin
        es_valid <= 1'b0;
    end
    else if (es_allowin) begin
        es_valid <= ds_to_es_valid;
    end

    if (ds_to_es_valid && es_allowin) begin
        ds_to_es_bus_r <= ds_to_es_bus;
    end
end

endmodule
